# Maintainer: Lyd√©ric Landry <lyderic@lyderic.com>
pkgname=freetube-lyderic
pkgver=0.23.1
pkgrel=1
pkgdesc="An Open Source YouTube app for privacy"
arch=('x86_64')
depends=('zlib')
makedepends=('7zip' 'fuse')
url="https://freetubeapp.io/"
license=('AGPL3')
provides=('freetube.AppImage')
conflicts=('freetube.AppImage')
freetubeAppImage="freetube-${pkgver}-amd64.AppImage"
noextract=("${freetubeAppImage}")
source=("https://github.com/FreeTubeApp/FreeTube/releases/download/v${pkgver}-beta/${freetubeAppImage}")
sha256sums=('6d0bfcc05f6656e7ef9b1935f501722fb3d5254f29b307b3436dc42aedc80c5e')
options=("!strip")

prepare() {
	cd "${srcdir}"
	mv -iv "${freetubeAppImage}" "freetube.AppImage"
	7z e "${srcdir}/freetube.AppImage" usr/share/icons/hicolor/scalable/freetube.svg
	7z x "${srcdir}/freetube.AppImage" freetube.desktop
	sed -i 's@Exec=.*@Exec=/usr/bin/freetube.AppImage %U@' freetube.desktop	
}

package() {
	install -Dm755 "${srcdir}/freetube.AppImage" "${pkgdir}/usr/bin/freetube.AppImage"
	install -Dm644 "${srcdir}/freetube.desktop" "${pkgdir}/usr/share/applications/freetube.desktop"
	install -Dm644 "${srcdir}/freetube.svg" "${pkgdir}/usr/share/pixmaps/freetube.svg"
}
