# Maintainer: Lyd√©ric Landry <lyderic@lyderic.com>
pkgname=freetube-lyderic
pkgver=0.23.12
pkgrel=1
pkgdesc="An Open Source YouTube app for privacy"
arch=('x86_64')
depends=('zlib')
makedepends=('7zip' 'fuse')
url="https://freetubeapp.io/"
license=('AGPL3')
provides=('freetube.AppImage')
conflicts=('freetube.AppImage')
freetubeAppImage="freetube-${pkgver}-beta-amd64.AppImage"
noextract=("${freetubeAppImage}")
source=("https://github.com/FreeTubeApp/FreeTube/releases/download/v${pkgver}-beta/${freetubeAppImage}")
sha256sums=('2192afeea12f727f83044decbe0bdd92b0e98c78f6d22f9e613640419b16e44a')
options=("!strip")

prepare() {
	cd "${srcdir}"
	mv -iv "${freetubeAppImage}" "freetube.AppImage"
	7z e "${srcdir}/freetube.AppImage" usr/share/icons/hicolor/scalable/freetube.svg
	7z x "${srcdir}/freetube.AppImage" freetube.desktop
	sed -i 's@Exec=.*@Exec=/usr/bin/freetube.AppImage %U@' freetube.desktop	
}

package() {
	install -Dm755 "${srcdir}/freetube.AppImage" "${pkgdir}/usr/bin/freetube.AppImage"
	install -Dm644 "${srcdir}/freetube.desktop" "${pkgdir}/usr/share/applications/freetube.desktop"
	install -Dm644 "${srcdir}/freetube.svg" "${pkgdir}/usr/share/pixmaps/freetube.svg"
}
